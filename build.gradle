buildscript {

	ext {
        logstashLogbackEncoderVersion = '4.7'
        logbackVersion = '1.1.6'
        jacksonVersion = '2.8.0'
    }

	repositories {
		mavenCentral()
	}

	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:${dependencyManagementVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'

dependencyManagement {
    imports {
        mavenBom("org.springframework.cloud:spring-cloud-sleuth:${springCloudSleuthVersion}")
        mavenBom("org.springframework.cloud:spring-cloud-netflix:${springCloudNetflixVersion}")
    }
}

jar {
    baseName = 'camel-spring'
    version = '1.0-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}

jacoco {
    toolVersion = "${dependencyVersionJacoco}"
}

jacocoTestReport {
    reports {
        csv.enabled false
        xml.enabled true
        html.enabled true
    }
}

test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpFile = file("$buildDir/jacoco/classpathdumps")
    }
}

task dist(type: Zip) {
    // generate artefact zip file under build/distributions directory
    from jar.outputs.files
    // copy everything under directory
    from('scripts/') {
		fileMode = 0755
		into('scripts')
	}
    // copy a single file
	from 'appspec.yml'
}

springBoot {
	executable = true
}

dependencies {
	// Spring Boot
	compile "org.springframework.boot:spring-boot-starter:${springBootVersion}"
	compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
	compile "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"

    // Spring Cloud Sleuth
    compile "org.springframework.cloud:spring-cloud-starter-sleuth"
    // Spring Cloud Netflix
    compile "org.springframework.cloud:spring-cloud-starter-hystrix"

    // Zipkin client
    compile "org.springframework.cloud:spring-cloud-sleuth-zipkin:${springCloudSleuthZipkinVersion}"

    // dependencies error:
    // 'dependencies.dependency.systemPath' for com.sun:tools:jar must specify an absolute path but is
    // /home/tclement/plf-release-tools/tools/jdk1.7.0_67/jre/../lib/tools.jar in org.crashub:crash.shell:1.3.2
//	compile "org.springframework.boot:spring-boot-starter-remote-shell:${springBootVersion}"
//  compile files("${System.properties['java.home']}/../lib/tools.jar")
//  compile(files(((URLClassLoader) ToolProvider.getSystemToolClassLoader()).getURLs()))

	// Apache Camel
	compile "org.apache.camel:camel-spring-boot:${camelVersion}"
    // Failed to find this dependency
//	compile "org.apache.camel:camel-commands-spring-boot:${camelVersion}"

    // FastXML Jackson
    compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    // Jackson module auto-detect names of @JsonCreator annotated constructor, static factory methods.
    compile "com.fasterxml.jackson.module:jackson-module-paranamer:${jacksonVersion}"

	// Jolokia
	compile "org.jolokia:jolokia-core:${jolokiaVersion}"

	// Hawtio for Spring Boot
	compile "io.hawt:hawtio-springboot:${dependencyVersionHawtio}"

    // Cyclops React
    compile "com.aol.simplereact:cyclops-react:${dependencyVersionCyclopsReact}"

	// iText
    compile "com.lowagie:itext:${dependencyVersioniText}"

    // Logback
    // Turn back to version 1.16 due to defect http://jira.qos.ch/browse/LOGBACK-1158
    //  Caused by: java.lang.IllegalStateException: Logback configuration error detected:
    //  ERROR in ch.qos.logback.core.joran.action.NestedBasicPropertyIA - Unexpected aggregationType AS_BASIC_PROPERTY_COLLECTION
    compile "ch.qos.logback:logback-core:${logbackVersion}"
    compile "ch.qos.logback:logback-classic:${logbackVersion}"

    // LogStash Logback Encoder
    compile "net.logstash.logback:logstash-logback-encoder:${logstashLogbackEncoderVersion}"

    // Testing
	testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.14'
}
